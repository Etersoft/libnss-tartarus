from glob import glob
from os import path
from subprocess import Popen, PIPE

Import('env', 'rpc')

env = env.Clone()
env.Append(LIBS=['Ice'])
env['CCFLAGS'] += Popen(["/usr/bin/krb5-config", "--cflags"], stdout=PIPE).communicate()[0].split()
env['LIBS'] += Popen(["/usr/bin/krb5-config", "--libs"], stdout=PIPE).communicate()[0].split()
env['CPPPATH'] += ['.']

libs = [path.basename(str(i[0])) for i in (rpc,)]
libpath = [path.dirname(str(i[0])) for i in (rpc,)]

env.Append(LIBS=libs + ['json_spirit', 'boost_system'])
env['LIBPATH'] = libpath

ice = env.Ice('SysDB', '/usr/share/Tartarus/slice/SysDB/SysDB.ice')
env.Depends('ServerI.cpp', ice)

src = glob('*.cpp')
if 'SysDB.cpp' in src:
    src.remove('SysDB.cpp')
tnscd = env.Program('tnscd', src + [ice])

Return('tnscd')

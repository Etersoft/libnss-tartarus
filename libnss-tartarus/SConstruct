Environment(tools=['install'])
AddOption('--libdir',
      dest='libdir',
      type='string',
      nargs=1,
      action='store',
      default='/usr/lib',
      metavar='DIR',
      help='installation libdir')
AddOption('--libsysdir',
      dest='libsysdir',
      type='string',
      nargs=1,
      action='store',
      default='/lib',
      metavar='DIR',
      help='installation libsysdir')
env = Environment(LIBDIR = GetOption('libdir'), LIBSYSDIR = GetOption('libsysdir'))

env['BUILDERS']['Ice'] = Builder(action = 'cd `dirname $TARGET`; slice2cpp $SOURCE', src_suffix='.ice', suffix='.cpp')
env['CPPPATH'] = ['#.']

destdir = GetOption('install_sandbox')
destdir = destdir if destdir else '/'
env['DESTDIR'] = destdir
Alias('install', destdir)

if 'DEBUG' in ARGUMENTS and ARGUMENTS['DEBUG'] == 'yes':
    env['CPPFLAGS'] = ['-DDEBUG', '-g']
else:
    env['CCFLAGS'] = ['-O2']
Export('env')

rpc = SConscript('RPC/SConscript')
Export('rpc')

SConscript('NSCD/SConscript')
SConscript('nss/SConscript')
